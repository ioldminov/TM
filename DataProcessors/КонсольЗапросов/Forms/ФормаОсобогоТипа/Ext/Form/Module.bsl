

&НаКлиенте
Перем мСтруктураТипов;


&НаКлиенте
Процедура Инициализировать() Экспорт
	ИмяТипаНеопределено    = "Неопределено";
	ИмяТипаМоментВремени   = "МоментВремени";
	ИмяТипаГраница         = "Граница";
	ИмяТипаТаблицаЗначений = "ТаблицаЗначений";
	ИмяТипаДата            = "Дата";
	
	ИменаПолей = "ИмяТипа, Тип";
	
	мСтруктураТипов = Новый Структура();
	мСтруктураТипов.Вставить(ИмяТипаНеопределено, 
		Новый Структура(ИменаПолей, ИмяТипаНеопределено, Тип(ИмяТипаНеопределено)));
	мСтруктураТипов.Вставить(ИмяТипаМоментВремени, 
		Новый Структура(ИменаПолей, ИмяТипаМоментВремени, Тип(ИмяТипаМоментВремени)));
	мСтруктураТипов.Вставить(ИмяТипаГраница, 
		Новый Структура(ИменаПолей, ИмяТипаГраница, Тип(ИмяТипаГраница)));
	//мСтруктураТипов.Вставить(ИмяТипаТаблицаЗначений, 
	//	Новый Структура(ИменаПолей, ИмяТипаТаблицаЗначений, Тип(ИмяТипаТаблицаЗначений)));
	мСтруктураТипов.Вставить(ИмяТипаДата, 
		Новый Структура(ИменаПолей, ИмяТипаДата, Тип(ИмяТипаДата)));
	
	Список = Элементы.ТипПараметра.СписокВыбора;
	Список.Добавить(мСтруктураТипов.Неопределено.Тип, мСтруктураТипов.Неопределено.ИмяТипа);
	Список.Добавить(мСтруктураТипов.МоментВремени.Тип, мСтруктураТипов.МоментВремени.ИмяТипа);
	Список.Добавить(мСтруктураТипов.Граница.Тип, мСтруктураТипов.Граница.ИмяТипа);
		
КонецПроцедуры

&НаКлиенте
Процедура Завершить(Команда)
	Закрыть(ЗавершитьРедактирование(мСтруктураТипов));
КонецПроцедуры

&НаКлиенте
Процедура ТипПараметраПриИзменении(Элемент)
	УстановитьНастройкиТипа();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЗначениеЗаполнено(СтроковоеЗначениеПараметра) Тогда
		ТипПараметра = мСтруктураТипов.Неопределено.Тип;
	Иначе
		ЗаполнитьДанныеФормы(мСтруктураТипов);
		УстановитьНастройкиПоляГраницаЗначение();
	КонецЕсли;
	УстановитьНастройкиТипа();
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаданияГраницыПриИзменении(Элемент)
	УстановитьНастройкиПоляГраницаЗначение();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройкиТипа()
	СтраницыТипов = Элементы.ГруппаСтраницыТипов;
	СтраницыТипов.ТекущаяСтраница = СтраницыТипов.ПодчиненныеЭлементы["Страница" + ПолучитьИмяТипа()];
КонецПроцедуры


&НаКлиенте
Процедура УстановитьНастройкиПоляГраницаЗначение()
	ПолеГраницаЗначение = Элементы.ГраницаЗначение;
	Если СпособЗаданияГраницы = 0 Тогда
		ПолеГраницаЗначение.ОграничениеТипа = СтруктураДоступныхТипов.Дата;
		ПолеГраницаЗначение.ВыбиратьТип     = Ложь;
	ИначеЕсли СпособЗаданияГраницы = 1 Тогда
		ПолеГраницаЗначение.ОграничениеТипа = СтруктураДоступныхТипов.Документы;
		ПолеГраницаЗначение.ВыбиратьТип     = Истина;
	КонецЕсли;
	ГраницаЗначение = ПолеГраницаЗначение.ОграничениеТипа.ПривестиЗначение(ГраницаЗначение);
КонецПроцедуры


&НаКлиенте
Функция ПолучитьИмяТипа()
	Возврат Элементы.ТипПараметра.СписокВыбора.НайтиПоЗначению(ТипПараметра).Представление;
КонецФункции

// -------------------------
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СтруктураДоступныхТипов = Новый Структура("Дата, Документы");
	СтруктураДоступныхТипов.Дата = Новый ОписаниеТипов("Дата");
	СтруктураДоступныхТипов.Документы = Документы.ТипВсеСсылки();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФормы(СтруктураТипов)
	ПолученноеЗначение = ЗначениеИзСтрокиВнутр(СтроковоеЗначениеПараметра);
	ТипПараметра = ТипЗнч(ПолученноеЗначение);
	
	Если ТипПараметра = СтруктураТипов.Неопределено.Тип Тогда
		
	ИначеЕсли ТипПараметра = СтруктураТипов.МоментВремени.Тип Тогда
		МоментВремениСсылка = ПолученноеЗначение.Ссылка;
	ИначеЕсли ТипПараметра = СтруктураТипов.Граница.Тип Тогда
		ТипЗначенияГраницы = ТипЗнч(ПолученноеЗначение.Значение);
		Если ТипЗначенияГраницы = СтруктураТипов.МоментВремени.Тип Тогда
			ГраницаЗначение = ПолученноеЗначение.Значение.Ссылка;
			СпособЗаданияГраницы = 1;
		ИначеЕсли ТипЗначенияГраницы = СтруктураТипов.Дата.Тип Тогда
			ГраницаЗначение  = ПолученноеЗначение.Значение;
			СпособЗаданияГраницы = 0;
		КонецЕсли;
		Если ПолученноеЗначение.ВидГраницы = ВидГраницы.Включая Тогда
			ГраницаВидГраницы = 0;
		Иначе
			ГраницаВидГраницы = 1;
		КонецЕсли;
	//ИначеЕсли ТипПараметра = СтруктураТипов.ТаблицаЗначений.Тип Тогда
	
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Функция ЗавершитьРедактирование(СтруктураТипов)
	Результат = Неопределено;
	Если ТипПараметра = СтруктураТипов.Неопределено.Тип Тогда
		
	ИначеЕсли ТипПараметра = СтруктураТипов.МоментВремени.Тип Тогда
		Если ЗначениеЗаполнено(МоментВремениСсылка) Тогда
			Результат = МоментВремениСсылка.МоментВремени();
		КонецЕсли;
	ИначеЕсли ТипПараметра = СтруктураТипов.Граница.Тип Тогда
		Если ЗначениеЗаполнено(ГраницаЗначение) Тогда
			ЗначениеВидаГраницы = ?(ГраницаВидГраницы = 0, ВидГраницы.Включая, ВидГраницы.Исключая);
			Если ТипЗнч(ГраницаЗначение) = СтруктураТипов.Дата.Тип Тогда
				Результат = Новый Граница(ГраницаЗначение, ЗначениеВидаГраницы);
			Иначе
				Результат = Новый Граница(ГраницаЗначение.МоментВремени(), ЗначениеВидаГраницы);
			КонецЕсли;
		КонецЕсли;
	//ИначеЕсли ТипПараметра = СтруктураТипов.ТаблицаЗначений.Тип Тогда
	
	КонецЕсли;
	
	СтруктураРезультата = Новый Структура("ИмяПараметра, Результат, СтрокаТипРезультата");
	СтруктураРезультата.ИмяПараметра = ИмяПараметра;
	СтруктураРезультата.Результат = ЗначениеВСтрокуВнутр(Результат);
	СтруктураРезультата.СтрокаТипРезультата = Строка(ТипЗнч(Результат));
	
	Возврат СтруктураРезультата;
КонецФункции

