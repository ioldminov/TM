
Функция РазобратьПутьКОбъектуМетаданных(ПутьКДанным)
	
	Структура = Новый Структура;
	
	СоответствиеИмен = Новый Массив();
	СоответствиеИмен.Добавить("ТипОбъекта");
	СоответствиеИмен.Добавить("ВидОбъекта");
	СоответствиеИмен.Добавить("ПутьКДанным");
	СоответствиеИмен.Добавить("ИмяТаблЧасти");
	СоответствиеИмен.Добавить("ИмяРеквизита");
	
	Для индекс = 1 по 3 Цикл
		
		Точка = Найти(ПутьКДанным, ".");
		ТекущееЗначение = Лев(ПутьКДанным, Точка-1);
		Структура.Вставить(СоответствиеИмен[индекс-1], ТекущееЗначение);
		ПутьКДанным = Сред(ПутьКДанным, Точка+1);
		
	КонецЦикла;
	
	ПутьКДанным = СтрЗаменить(ПутьКДанным, "Реквизит.", "");
	
	Если Структура.ПутьКДанным = "ТабличнаяЧасть" Тогда
		
		Для индекс = 4 по 5  Цикл 
			
			Точка = Найти(ПутьКДанным, ".");
			Если Точка = 0 Тогда
				ТекущееЗначение = ПутьКДанным;
			Иначе
				ТекущееЗначение = Лев(ПутьКДанным, Точка-1);
			КонецЕсли;
			
			Структура.Вставить(СоответствиеИмен[индекс-1], ТекущееЗначение);
			ПутьКДанным = Сред(ПутьКДанным,  Точка+1);
			
		КонецЦикла;
		
	Иначе
		
		Структура.Вставить(СоответствиеИмен[3], "");
		Структура.Вставить(СоответствиеИмен[4], ПутьКДанным);
		
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции // РазобратьПутьКОбъектуМетаданных()

Функция ПолучитьМассивРеквизитовСоставаКритерияОтбора(ВидДок, ОбъектыМетаданных) Экспорт 
        
	МассивИменРеквизитов = Новый Массив;
	Для Каждого ЭлементСостава ИЗ Метаданные.КритерииОтбора.Задачи.Состав Цикл
		ПутьКДанным = ЭлементСостава.ПолноеИмя();
		СтруктураПутьКДанным = РазобратьПутьКОбъектуМетаданных(ПутьКДанным);
		
		Если СтруктураПутьКДанным.ВидОбъекта = ВидДок Тогда 
			МассивИменРеквизитов.Добавить(СтруктураПутьКДанным.ИмяРеквизита);
		КонецЕсли;
	КонецЦикла;
	
	РеквизитыОбъекта = ОбъектыМетаданных[ВидДок].Реквизиты;
	
	МассивРеквизитов = Новый Массив;
	Для Каждого ИмяРеквизита Из МассивИменРеквизитов Цикл
		МассивРеквизитов.Добавить(РеквизитыОбъекта[ИмяРеквизита]);
	КонецЦикла;
	
	Возврат МассивРеквизитов;
	
КонецФункции

Функция ПолучитьТекстЗапросаККритериюОтбора(ИмяКритерияОтбора, ПараметрОтбора, мИмяПараметра, мИмяПоля, ФильтрПоРеквизиту = Неопределено, ДобавлятьРеквизиты = Неопределено) Экспорт 
	
	ТекстЗапроса = ""; 
	ТекстОкончание = "";
	Если мИмяПоля = "" Тогда 
		ИмяПоля = "";
	Иначе
		ИмяПоля = " КАК " + мИмяПоля;
	КонецЕсли;
	
	ТипПараметрОтбора = ТипЗнч(ПараметрОтбора);
	Для Каждого ЭлементСостава ИЗ Метаданные.КритерииОтбора[ИмяКритерияОтбора].Состав Цикл
		
		Если Не ЭлементСостава.Тип.СодержитТип(ТипПараметрОтбора) Тогда
			Продолжить;
		КонецЕсли;
		
		ПутьКДанным = ЭлементСостава.ПолноеИмя();
		СтруктураПутьКДанным = РазобратьПутьКОбъектуМетаданных(ПутьКДанным);
		
		ИмяОбъекта = СтруктураПутьКДанным.ТипОбъекта + "." + СтруктураПутьКДанным.ВидОбъекта;
		
		Если НЕ ФильтрПоРеквизиту = Неопределено ИЛИ НЕ ДобавлятьРеквизиты = Неопределено Тогда
			Если СтруктураПутьКДанным.ТипОбъекта = "Документ" Тогда
				ОбъектМетаданных = Метаданные["Документы"];
			ИначеЕсли СтруктураПутьКДанным.ТипОбъекта = "Справочник" Тогда
				ОбъектМетаданных = Метаданные["Справочники"];
			ИначеЕсли СтруктураПутьКДанным.ТипОбъекта = "Задача" Тогда
				ОбъектМетаданных = Метаданные["Задачи"];
			КонецЕсли;
		Иначе
			ОбъектМетаданных = Неопределено;
		КонецЕсли;
		Если НЕ ФильтрПоРеквизиту = Неопределено И ОбъектМетаданных[СтруктураПутьКДанным.ВидОбъекта].Реквизиты.Найти(ФильтрПоРеквизиту) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяСтрокаГДЕ = "ГДЕ " + СтруктураПутьКДанным.ВидОбъекта + "." +СтруктураПутьКДанным.ИмяРеквизита + " = &"+мИмяПараметра+"";
		
		ИмяТЧ = Лев(СтруктураПутьКДанным.ИмяРеквизита, Найти(СтруктураПутьКДанным.ИмяРеквизита, ".")-1);
		ИмяРеквизита = Лев(СтруктураПутьКДанным.ИмяРеквизита, Найти(СтруктураПутьКДанным.ИмяРеквизита, ".")-1);
		ТекстЗапроса = ТекстЗапроса + ?(ТекстЗапроса = "", "ВЫБРАТЬ", "ОБЪЕДИНИТЬ
		|ВЫБРАТЬ") + "
		|" + СтруктураПутьКДанным.ВидОбъекта +".Ссылка "+ ИмяПоля 
		+?(ДобавлятьРеквизиты = Неопределено,"",", "+ДополнитьТекстЗапросаРеквизитами(ДобавлятьРеквизиты,СтруктураПутьКДанным.ВидОбъекта,ОбъектМетаданных))
		+" ИЗ " + ИмяОбъекта + ?(НЕ ПустаяСтрока(СтруктураПутьКДанным.ИмяТаблЧасти),"." + СтруктураПутьКДанным.ИмяТаблЧасти,"") + " КАК " + СтруктураПутьКДанным.ВидОбъекта + "
		|" + СтрЗаменить(ТекущаяСтрокаГДЕ, "..", ".") + "
		|";
		
		ИмяПоля = "";
	КонецЦикла;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ДополнитьТекстЗапросаРеквизитами(Знач ДобавлятьРеквизиты, ВидОбъекта, ОбъектМетаданных)
	
	Результат = "";
	
	ДобавлятьРеквизиты = СтрЗаменить(СтрЗаменить(ДобавлятьРеквизиты," ",""),",",Символы.ПС);
	ЧислоРеквизитов = СтрЧислоСтрок(ДобавлятьРеквизиты);
	Для СчСтрок = 1 По ЧислоРеквизитов Цикл
		Реквизит = СокрЛП(СтрПолучитьСтроку(ДобавлятьРеквизиты,СчСтрок));
		
		РеквизитСуществет = НЕ ОбъектМетаданных[ВидОбъекта].Реквизиты.Найти(Реквизит) = Неопределено;
		Если Не РеквизитСуществет Тогда //попробуем получить из станартных реквизитов
			Для каждого СтандартныйРеквизит Из ОбъектМетаданных[ВидОбъекта].СтандартныеРеквизиты Цикл
				Если СтандартныйРеквизит.Имя = Реквизит Тогда
					РеквизитСуществет = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли; 
		
		Результат = Результат+?(НЕ РеквизитСуществет,"NULL ",ВидОбъекта+".Ссылка."+Реквизит)+" КАК "+Реквизит+?(СчСтрок = ЧислоРеквизитов,"",",")
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ДополнитьТекстЗапросаРеквизитами()

Функция ПолучитьКоличествоПодчиненныхСтрок(Строки)
	
	КоличествоПодчиненныхСтрок = Строки.Количество();
	
	Для Каждого ТекСтрока Из Строки Цикл
 		КоличествоПодчиненныхСтрок = КоличествоПодчиненныхСтрок + ПолучитьКоличествоПодчиненныхСтрок(ТекСтрока.Строки);
	КонецЦикла;
	
	Возврат КоличествоПодчиненныхСтрок;
		
КонецФункции


Функция ПолучитьКоличествоЭлементовДерева(Дерево) Экспорт
	
	КоличествоПодчиненныхСтрок = ПолучитьКоличествоПодчиненныхСтрок(Дерево.Строки);
	
	Возврат КоличествоПодчиненныхСтрок;
		
КонецФункции
