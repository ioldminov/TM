
Функция НайтиЗаданиеПоИдентификатору(Знач ИдентификаторЗадания)
	
	Если ТипЗнч(ИдентификаторЗадания) = Тип("Строка") Тогда
		ИдентификаторЗадания = Новый УникальныйИдентификатор(ИдентификаторЗадания);
	КонецЕсли;
	
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	Возврат Задание;
	
КонецФункции

// Проверяет состояние фонового задания по переданному идентификатору.
// При аварийном завершении задания вызывает исключение.
//
// Параметры:
//  ИдентификаторЗадания - УникальныйИдентификатор - идентификатор фонового задания. 
//
// Возвращаемое значение:
//  Булево - состояние выполнения задания.
// 
Функция ЗаданиеВыполнено(Знач ИдентификаторЗадания) Экспорт
	
	Задание = НайтиЗаданиеПоИдентификатору(ИдентификаторЗадания);
	
	Если Задание <> Неопределено
		И Задание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОперацияНеВыполнена = Истина;
	ПоказатьПолныйТекстОшибки = Ложь;
	мОписаниеОшибки = "";
	Если Задание = Неопределено Тогда
		мОписаниеОшибки = "Длительные операции.Фоновое задание не найдено";
	Иначе
		Если Задание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
			ОшибкаЗадания = Задание.ИнформацияОбОшибке;
			Если ОшибкаЗадания <> Неопределено Тогда
				ПоказатьПолныйТекстОшибки = Истина;
			КонецЕсли;
		ИначеЕсли Задание.Состояние = СостояниеФоновогоЗадания.Отменено Тогда
			мОписаниеОшибки = "Длительные операции.Фоновое задание отменено администратором";
		Иначе
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ПоказатьПолныйТекстОшибки Тогда
		ТекстОшибки = КраткоеПредставлениеОшибки(Задание.ИнформацияОбОшибке);
		ВызватьИсключение(ТекстОшибки);
	ИначеЕсли ОперацияНеВыполнена Тогда
		ВызватьИсключение("Не удалось выполнить данную операцию. Подробности: " + мОписаниеОшибки);
	КонецЕсли;
	
КонецФункции

//////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ

Процедура ВыполнитьДлительнуюОперацию(Длительность, АдресВХранилище) Экспорт
	
	// Имитация продолжительного действия (Длительность сек.).
	ДатаНачалаОперации = ТекущаяДата();
	Пока ТекущаяДата() - ДатаНачалаОперации < Длительность Цикл
		
	КонецЦикла;
	ДатаОкончанияОперации = ТекущаяДата();
	
	Струк = Новый Структура("ДатаНачалаОперации, ДатаОкончанияОперации", ДатаНачалаОперации, ДатаОкончанияОперации);
	ПоместитьВоВременноеХранилище(Струк, АдресВХранилище);
	
КонецПроцедуры

#Область Новости

Функция ПолучитьТаблицуНепрочитанныхНовостей()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	АдресацияНовостей.Период,
	|	АдресацияНовостей.Объект,
	|	АдресацияНовостей.Адресат
	|ИЗ
	|	РегистрСведений.АдресацияНовостей КАК АдресацияНовостей
	|ГДЕ
	|	АдресацияНовостей.Адресат = &Адресат
	|	И АдресацияНовостей.Прочитана = ЛОЖЬ";
	Запрос.УстановитьПараметр("Адресат", ПараметрыСеанса.ТекущийПользователь);
	
	Возврат Запрос.Выполнить().Выгрузить();
		
КонецФункции

Функция ПолучитьАдресХранилищаНовостейНаСервере(ТаблицаНовостей = Неопределено)
	
	Если ТаблицаНовостей = Неопределено Тогда 
		ТаблицаНовостей = ПолучитьТаблицуНепрочитанныхНовостей();
	КонецЕсли;
    
    мАдресХранилищаНовостей = ПоместитьВоВременноеХранилище(ТаблицаНовостей, Новый УникальныйИдентификатор);
    
    Возврат мАдресХранилищаНовостей;
        
КонецФункции

Функция ПроверитьНаличиеНовыхНовостей(АдресДанныхВХранилище, АдресХранилищаНовостей)
	
	Результат = Ложь;
	
	ТаблицаНепрочитанныхНовостей = ПолучитьТаблицуНепрочитанныхНовостей();
	
	мКешДанныхНовостей = ПолучитьИзВременногоХранилища(АдресХранилищаНовостей);
	//мКешДанныхНовостей = Новый ТаблицаЗначений;
	мКешДанныхНовостей.Индексы.Добавить("Период, Объект, Адресат");
	
	мОтбор = Новый Структура("Период, Объект, Адресат");
	Для Каждого ТекСтрока Из ТаблицаНепрочитанныхНовостей Цикл
		
		//ЗаполнитьЗначенияСвойств(мОтбор, ТекСтрока);
		мОтбор.Период = ТекСтрока.Период;
		мОтбор.Объект = ТекСтрока.Объект;
		мОтбор.Адресат = ТекСтрока.Адресат;
		
		НайденныеСтроки = мКешДанныхНовостей.НайтиСтроки(мОтбор);
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			Результат = Истина;
			УдалитьИзВременногоХранилища(АдресХранилищаНовостей);
			АдресХранилищаНовостей = ПолучитьАдресХранилищаНовостейНаСервере(ТаблицаНепрочитанныхНовостей);
			
			НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(ТекСтрока.Объект);
			
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(Результат, АдресДанныхВХранилище);
	
КонецФункции

#КонецОбласти