
&НаКлиенте
Процедура Генерировать(Команда)
	
	ГСЧ = Новый ГенераторСлучайныхЧисел(ТекущаяУниверсальнаяДатаВМиллисекундах());
	Н = СтрДлина(ИспользуемыеСимволы);
	Результат = "";
	
	Пока СтрДлина(Результат) < Длина Цикл
		НекийСимвол = Сред(ИспользуемыеСимволы,(ГСЧ.СлучайноеЧисло(1, Н)), 1);
		Результат = Результат + НекийСимвол;	
	КонецЦикла;
	
	// Скопируем результат в буфер
	COMОбъект = Новый COMОбъект("htmlfile"); 
	COMОбъект.ParentWindow.ClipboardData.Setdata("Text", Результат); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииУсловийГенерации(Элемент)
	
	ИспользуемыеСимволы = "";
	
	Если Цифры Тогда
		ИспользуемыеСимволы = ИспользуемыеСимволы + "0123456789";
	КонецЕсли; 
	
	Если Латиница Тогда
		СтрокаЛатиница = "ABCDEFGHIKLMNOPQRSTVXYZ";
		Если ВариантРегистра = 0 Тогда
			ИспользуемыеСимволы = ИспользуемыеСимволы + НРег(СтрокаЛатиница) + ВРег(СтрокаЛатиница);
		ИначеЕсли ВариантРегистра = 1 Тогда 
			ИспользуемыеСимволы = ИспользуемыеСимволы + ВРег(СтрокаЛатиница);			
		Иначе
			ИспользуемыеСимволы = ИспользуемыеСимволы + НРег(СтрокаЛатиница);			
		КонецЕсли; 		
	КонецЕсли;
	
	Если Кириллица Тогда
		СтрокаКириллица = "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ";
		Если ВариантРегистра = 0 Тогда
			ИспользуемыеСимволы = ИспользуемыеСимволы + НРег(СтрокаКириллица) + ВРег(СтрокаКириллица);
		ИначеЕсли ВариантРегистра = 1 Тогда 
			ИспользуемыеСимволы = ИспользуемыеСимволы + ВРег(СтрокаКириллица);			
		Иначе
			ИспользуемыеСимволы = ИспользуемыеСимволы + НРег(СтрокаКириллица);			
		КонецЕсли;
	КонецЕсли; 
	
	Если Спецсимволы Тогда
		ИспользуемыеСимволы = ИспользуемыеСимволы + "!'""№;%:?*()-_+=`~@#$%^&<>/|\{}[]";
	КонецЕсли;
	
	Если ИсключитьПроблемныеСимволы Тогда
		СтрокаПроблемныеСимволы = "LlIi1oO0"+"оО";// Группа латиница + кириллица
		
		Для Индекс = 1 По СтрДлина(СтрокаПроблемныеСимволы) Цикл
			ИспользуемыеСимволы = СтрЗаменить(ИспользуемыеСимволы, Сред(СтрокаПроблемныеСимволы,Индекс,1),"");
		КонецЦикла;
		
	КонецЕсли; 
	
	Если ИсключитьСлужебныеСимволы Тогда
		СтрокаПроблемныеСимволы = ":<=>?@;[\]^_`""'";
		
		Для Индекс = 1 По СтрДлина(СтрокаПроблемныеСимволы) Цикл
			ИспользуемыеСимволы = СтрЗаменить(ИспользуемыеСимволы, Сред(СтрокаПроблемныеСимволы,Индекс,1),"");
		КонецЦикла;
		
	КонецЕсли; 
	
	ИспользуемыеСимволы = СтрЗаменить(ИспользуемыеСимволы, " ", "");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Установим настройки по умолчанию
	Латиница = Истина;
	Цифры = Истина;
	Спецсимволы = Истина;
	ВариантРегистра = 0;
	ИсключитьСлужебныеСимволы = Истина;
	ИсключитьПроблемныеСимволы = Истина;
	Длина = 8;
	
	ПриИзмененииУсловийГенерации(Неопределено);
	
КонецПроцедуры
