
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Запись.Исполнитель) И ЗначениеЗаполнено(Запись.Задача) Тогда 
		Запись.Исполнитель = Запись.Задача.Исполнитель;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Запись.Период) Тогда 
		Запись.Период = ?(ЗначениеЗаполнено(Запись.ДатаНачалаПлан), Запись.ДатаНачалаПлан, ТекущаяДата());
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Запись.Задача) Тогда 
		Запись.Задача = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПланПриИзменении(Элемент)
	
	Если Запись.ДатаНачалаПлан > Запись.ДатаОкончанияПлан Тогда 
		Запись.ДатаОкончанияПлан = Запись.ДатаНачалаПлан;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиЗадачуНаКлиенте(Знач НомерЗадачи, ОповещатьОбОшибке = Истина)
	
	ЗадачаСсылка = Общий.офПолучитьЗадачуПоНомеру(НомерЗадачи);
	
	Если ЗадачаСсылка <> Неопределено Тогда
		Запись.Задача = ЗадачаСсылка;
	Иначе
		Если ОповещатьОбОшибке Тогда
			Сообщить("Не удалось найти задачу по указанному номеру");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиЗадачу(Команда)
	
	НайтиЗадачуНаКлиенте(Запись.Задача);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПриОткрытииНаСервере();
	ОтобразитьСутьЗадачи();

	ПодключитьОбработчикОжидания("УстановитьТекущийЭлементЗадача", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущийЭлементЗадача()
	
	ЭтаФорма.ТекущийЭлемент = ЭтаФорма.Элементы.Задача;
	
КонецПроцедуры



&НаСервере
Процедура ПриОткрытииНаСервере()
	
	Если Не ЗначениеЗаполнено(Запись.Задача) Тогда 
		Запись.Задача = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадачаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Запись.Задача = "";
	
	ЗадачаПриИзменении(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадачаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	НайтиЗадачуНаКлиенте(Текст, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадачаПриИзменении(Элемент)
	
	ОтобразитьСутьЗадачи();
	
КонецПроцедуры

&НаСервере
Функция ЭтоДокументЗадача()

	Возврат ТипЗнч(Запись.Задача) = Тип("ДокументСсылка.Задача");
		
КонецФункции

&НаСервере
Функция ПолучитьСутьЗадачи()
		
	Возврат СокрЛП(Запись.Задача.Суть);
	
КонецФункции


&НаКлиенте
Процедура ОтобразитьСутьЗадачи()
	
	Если ЭтоДокументЗадача() Тогда
		Элементы.СутьЗадачи.Видимость = Истина;
		СутьЗадачи = ПолучитьСутьЗадачи();
	Иначе 
		Элементы.СутьЗадачи.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СдвинутьНаДень(НаправлениеВперед = Истина)
	
	Запись.ДатаНачалаПлан 		= Общий.офИзменитьДатуНаДень(Запись.ДатаНачалаПлан, НаправлениеВперед);
	Запись.ДатаОкончанияПлан 	= Общий.офИзменитьДатуНаДень(Запись.ДатаОкончанияПлан, НаправлениеВперед);
		
КонецПроцедуры


&НаКлиенте
Процедура СдвинутьНаДеньВперед(Команда)
	
	СдвинутьНаДень(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СдвинутьНаДеньНазад(Команда)
	
	СдвинутьНаДень(Ложь);
	
КонецПроцедуры


